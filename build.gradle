buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.serenity-bdd:serenity-gradle-plugin:1.9.4"
        classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.6"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.sherter.google-java-format'


def serenityVersion = "1.9.4"

repositories {
    mavenCentral()
}

dependencies {
    compile "net.serenity-bdd:serenity-core:$serenityVersion"
    compile "net.serenity-bdd:serenity-cucumber:$serenityVersion"
    compile "org.assertj:assertj-core:1.7.0"
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.2'
    compile group: 'org.slf4j', name:'slf4j-log4j12', version: '1.7.2'
    testCompile "junit:junit:4.12"
}

sonarqube {
    properties {
        property "sonar.projectKey", "BDDCoreSuraEmpresariales"
        property "sonar.projectName", "BDDCoreSuraEmpresariales"
        property "sonar.projectVersion", "1.0"
        property "sonar.sources", "src/main"
        property "sonar.java.binaries", "build"
        property "sonar.test", "src/test"
    }
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task icTest(type: Test) {
    def env = (System.getenv('ENV') ?: "dllo").trim()
    def app = (System.getenv('APP') ?: "ejemplos").trim()
    def propertyValue = project.property("${app}TestFilter").toString().trim()
    def filterRute = "${propertyValue}.${env}.*"

    logger.lifecycle("Variables de ejecucion para icTest")
    logger.lifecycle("env:$env, app:$app, propertyValue:$propertyValue, filterRute:$filterRute")

    // Si ENV est√° vacio se hace el set en el valor por defecto
    systemProperties = ['ENV': env]

    filter {
        includeTestsMatching filterRute
        failOnNoMatchingTests = false
    }
    maxParallelForks = Runtime.runtime.availableProcessors()
}

compileJava.dependsOn 'googleJavaFormat'
compileJava.mustRunAfter 'sonarqube'
gradle.startParameter.continueOnFailure = true